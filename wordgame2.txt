40 REM 64S1B     WORD ZAPPER I                      COPYRIGHT MICROGRAMS 1983 {$cc}
49 DIMA$(1,236),W$(9),D(9),E$(12),C$(11),E1$(12,1),A(9):POKE54296,15:PRINTCHR$(142)
50 POKE53280,5:POKE53281,1:PRINT"{clear}{down:12}{right:9}{blue}{right:7}WORKING"
51 FORI=0TO236:READA$(0,I),A$(1,I):NEXT
52 B$="{right}                                     {right:2}"
53 R1$="                    {right:20}":R2$="               "
59 FORI=0TO11:C$(I)="":NEXT:FORI=0TO12:E1$(I,0)="":E1$(I,1)="":E$(I)="":NEXT
60 WC=0:W=0:W1=0:S=0:M=0:E1=0:E=0:C=0:Q=0:DD=1:F2=0:F1=0:N$="":I$="":GOTO1000
70 PRINT"{green}{rvrs on} {$a2:15} "SPC(23)" {rvrs off}               {rvrs on} "SPC(23);
71 PRINT"                 ":RETURN
73 FORI=1TO10:GETG$:NEXT:RETURN
75 PRINT"{home}{down:4}{right:3}{blue}WATCH FOR A WORD"SPC(24)"TO FLASH IN THIS"SPC(30)"BOX.":RETURN
76 PRINT"{home}{down:11}{right:4}{red}NOW YOU SPELL"SPC(27)"THE SAME WORD.":GOSUB79:RETURN
77 PRINT"{home}{down:5}{right:6}{blue}WATCH AGAIN":RETURN
78 PRINT"{home}{down:11}{right:7}{red}TRY AGAIN":GOSUB79:RETURN
79 PRINT"{down}{right:3}THEN PUSH {rvrs on}RETURN{rvrs off}.{down:2}{left:3}":RETURN
80 GOSUB600:PRINT"{home}{down:5}"SPC(22);:FORI=1TO4:GOSUB601
81 PRINT"  *********  {left:13}";:FORJ=1TOTT:NEXT:GOSUB606
82 PRINT"             {left:13}";:FORJ=1TOTT:NEXT:NEXT
83 PRINTMID$(B$,1,INT((13-LEN(C$(C)))/2))C$(C):FORJ=1TOTT:NEXT
84 PRINT"{home}{down:5}"SPC(22)"  *********  ":GOSUB605:RETURN
100 WC=WC+1:K1=0:IFINT(RND(1)*3)=0THENK1=1
101 K2=INT(RND(1)*237):IFLEFT$(A$(K1,K2),1)="+"THEN100
102 A(W)=K2
103 W$(W)=A$(K1,K2)+"   ":A$(K1,K2)="+"+A$(K1,K2):D(W)=K1:DD=LEN(W$(W)):RETURN
200 PRINT"{home}{down:11}"SPC(39)CHR$(20)"{right:2}{up}{insert}"MID$(W$(W),DD,1):P1=PEEK(1499)
201 P2=PEEK(1500):IFP1<32ANDPP=0THENPP=1:GOTO205
202 IFNOT(P2=32ANDPP=1)THEN205
203 IFD(W1)=1THENF3=1:Z$=W$(W1):Z=A(W1)
204 PP=0:W1=W1+1:IFW1=10THENW1=0
205 DD=DD-1:IFDD>0THEN209
207 W=W+1:IFW=10THENW=0
208 GOSUB100
209 RETURN
300 PRINT"{home}{down:10}"SPC(34)"{red}{rvrs on}{$cd}{down}{left}{$c0}{$dd} '{down}{left}{$cd}{rvrs off}{up:2}{left:4} {down}{left}      {down}{left:3} {up}{left:4}{black}";:RETURN
310 PRINT"{home}{down:10}"SPC(34)"{red}{rvrs on} {down}{left}    {down}{left} {rvrs off}{up:2}{left:4} {down}{left}      {down}{left:3} {up}{left:4}{black}";:RETURN
320 PRINT"{home}{down:10}"SPC(34)"{red}{rvrs on}*{down}{left}****{down}{left}*{rvrs off}{up:2}{left:4} {down}{left}      {down}{left:3} {up}{left:4}{black}";:RETURN
330 PRINT"{home}{down:10}"SPC(34)"{red}{rvrs on}{$bf}{down}{left}{$bf}{down}{left}{$bf}{up}{$bf:3}{down}{left}{$bf}{rvrs off}{up:2}{left:4} {down}{left} {down}{left} {up}     {down}{left:3} {up}{left:4}{black}";
331 RETURN
400 I=0:PP=0
401 PRINT" {left:2}";:IFPEEK(1497-I)<32THENI=I+1:GOTO401
402 RETURN
500 GOSUB600:PRINT"{home}{down:9}"SPC(33);:FORI=0TO10:PRINT"{green}{$a6}{down}{left} {down}{left} {down}{left} {down}{left}{$a6}{up:4}{left}";
501 GOSUB602:FORJ=0TO100:NEXT:PRINT"{red}{rvrs on}S{down}{left}T{down}{left}O{down}{left}P{down}{left}!{rvrs off}{up:4}{left}";:GOSUB604
502 FORJ=0TO100:NEXT:NEXT:GOSUB605:RETURN
600 POKE54277,129:POKE54276,17:POKE54278,136:RETURN
601 POKE54273,34:POKE54272,75:RETURN
602 POKE54273,45:POKE54272,198:RETURN
603 POKE54273,57:POKE54272,172:RETURN
604 POKE54273,22:POKE54272,227:RETURN
605 POKE54277,0:POKE54276,0:POKE54272,0:POKE54273,0:RETURN
606 POKE54273,0:POKE54272,0:RETURN
607 POKE54273,45:POKE54272,198:FORK=0TO50:NEXT:POKE54272,0:POKE54273,0:RETURN
1000 PRINT"{clear}"SPC(54)"{blue}WORD ZAPPER I"SPC(109)"{red}COPYRIGHT"SPC(28)"{$a2:13}";
1001 PRINT"{$a2}"SPC(24)"{rvrs on}    MICROGRAMS    "SPC(24)"{$a2:14}{rvrs off}"SPC(31)"1983{down:2}"
1002 PRINT"{black}    PLEASE ENTER YOUR NAME"SPC(58)"AND PUSH {red}{rvrs on}RETURN{rvrs off}{black}.{right:8}{black}";
1003 PRINT" {left}";:FORI=1TO100:NEXT
1004 GETG$:IFNOTG$=""THEN1006
1005 PRINT"*{left}";:FORI=1TO100:NEXT:GOTO1003
1006 IFASC(G$)=13ANDLEN(N$)>0THEN1011
1007 IFASC(G$)=20ANDLEN(N$)>0THENN$=MID$(N$,1,LEN(N$)-1):PRINT"{left} {left}";:GOTO1005
1008 IFASC(G$)<65ORASC(G$)>90THEN1005
1009 N$=N$+G$:PRINTG$;:IFLEN(N$)>9THEN1011
1010 GOTO1005
1011 PRINT:PRINT"{up:3}"B$B$B$B$"{up:3}"
1012 PRINT"{right:4}{black}O.K. "N$",{down}":PRINT"{right:5}WHEN THE WORDS MOVE ACROSS THE{right:5}";
1013 PRINT"{right:5}SCREEN, USE THE SPACE BAR TO{right:12}"CHR$(34)"ZAP"CHR$(34);
1014 PRINT" MISSPELLED WORDS. IF YOU{right:10}ARE WRONG ON 12, THEN IT IS"
1015 PRINT"{right:5}TIME TO STOP AND PRACTICE YOUR{right:10}SPELLING. GET 150 RI";
1016 PRINT"GHT TO WIN. {right:6}{down}CHOOSE LEVEL {red}{rvrs on}1{rvrs off}, {rvrs on}2{rvrs off}, {rvrs on}3{rvrs off}, {black}OR {red}{rvrs on}I{rvrs off}{black}MPOSSIBLE."
1017 GETG$:IFG$=""THEN1017
1018 L=VAL(G$):IFG$="I"THENL=4
1019 IFL<1ORL>4THEN1017
2000 PRINT"{clear}"SPC(6)"{red}{$a2:7}"SPC(13)"{blue}{$a2:8}"SPC(12)"{red}{rvrs on} SCORE "SPC(13);
2001 PRINT"{blue}MISTAKES"SPC(12)"{red}       {rvrs off}"SPC(13)"{blue}{$a5}      {$a7}"SPC(12)"{red}{$a5}     {$a7}";
2002 PRINTSPC(13)"{blue}{$a5}      {$a7}"SPC(12)"{red}{$a3:7}"SPC(13)"{blue}{$a3:8}{down:4}"
2003 PRINT"{green} {$a6:38}{down:3}{right} {$a6:22}";
2004 PRINT"{$a6:16}{up:3}{left:4}{black}{$df}{ctrl pound}{down:2}{left:2}{rvrs on}{ctrl pound}{$df}{down:10}"
2005 PRINT"USE SPACE BAR TO "CHR$(34)"ZAP"CHR$(34)" MISSPELLED WORDS{home}":GOSUB100
2010 GETG$:IFNOTG$=""THEN2020
2011 T=T+1:IFT<QTHEN2010
2012 T=0:GOSUB200:IFWC>150THEN6000
2013 IFF3>0THEN2026
2014 IFF1>0THEN2010
2015 F2=F2+1:IFF2=10THENQ=10*(4-L):F1=1
2016 GOTO2010
2020 IFNOTG$=" "ORPP=0THEN2011
2021 GOSUB600:FORI1=1TO10:GOSUB601:ONINT(RND(1)*5)+1GOSUB300,310,320,330,320
2022 GOSUB602:NEXT:GOSUB400:IFD(W1)=1THEN2030
2023 E=E+1:E$(E)=MID$(W$(W1),1,LEN(W$(W1))-3):M=M+1:IFM=7THENM=41
2024 GOSUB604:PRINT"{home}"SPC(106+M)"*":IFM=46THENGOSUB500:GOTO3000
2025 GOSUB605:GOTO2040
2026 F3=0:E1=E1+1:E1$(E1,0)=MID$(Z$,1,LEN(Z$)-3):E1$(E1,1)=STR$(Z)
2027 M=M+1:IFM=7THENM=41
2028 GOSUB600:GOSUB604:PRINT"{home}"SPC(106+M)"*":IFM=46THENGOSUB500:GOTO3000
2029 GOSUB605:GOTO2010
2030 GOSUB600:GOSUB601:S=S+15*LEN(W$(W1)):IFS>99999THENS=0
2031 S$=STR$(S):FORI=LEN(S$)TO4:S$=" "+S$:NEXT
2032 PRINT"{home}{down:3}{right:7}"MID$(S$,2):GOSUB605
2040 PP=0:W1=W1+1:IFW1<10THEN2010
2041 W1=0:GOTO2010
3000 CC=E1+E:IFE1=0THEN3010
3001 PRINT"{home}{down:15}{right:2}{blue}THESE WORDS WERE MISSPE";
3002 PRINT"LLED, BUT{right:8}YOU DID NOT ZAP THEM:{black}"
3003 FORI=1TOE1STEP2:PRINT"{right:7}"E1$(I,0):PRINT"{up}"SPC(22)E1$(I+1,0):NEXT
3004 PRINT"{home}{down:23} {left}"B$"{left:2} {up}"SPC(19)"{red}{rvrs on}PUSH{down}{left:5}RETURN{black}{home}"
3005 GETG$:IFG$=""THEN3005
3006 IFNOTASC(G$)=13THEN3005
3010 IFE=0THEN3030
3011 PRINT"{home}{down:15}"B$B$B$B$B$B$B$B$"{home}"
3012 PRINT"{home}{down:15}{right:2}{blue}THESE WORDS WERE SPELLE";
3013 PRINT"D CORRECTLY,{right:5}BUT YOU ZAPPED THEM:{black}"
3014 FORI=1TOESTEP2:PRINT"{right:7}"E$(I):PRINT"{up}"SPC(25)E$(I+1):NEXT
3015 PRINT"{home}{down:23} {left}"B$"{left:2} {up}"SPC(19)"{red}{rvrs on}PUSH{down}{left:5}RETURN{black}{home}"
3016 GETG$:IFG$=""THEN3016
3017 IFNOTASC(G$)=13THEN3016
3030 IFE>0THENFORI=0TOE-1:C$(I)=E$(I+1):NEXT
3031 IFE1=0THEN4000
3032 II=0:FORI=ETOCC-1:II=II+1:Z=VAL(E1$(II,1)):C$(I)=A$(0,Z)
3033 IFLEFT$(C$(I),1)="+"THENC$(I)=MID$(C$(I),2)
3034 NEXT
4000 PRINT"{clear}{down:4}"SPC(20);:GOSUB70:GOSUB84:PRINT"{home}{down:10}"SPC(20);:GOSUB70
4001 TT=75:GOSUB75:FORI=1TO1500:NEXT:GOSUB80:FORI=1TO500:NEXT:GOSUB76
4016 PRINT"{home}{down:11}"SPC(22);
4017 PRINT" {left}";:FORI=1TO100:NEXT
4018 GETG$:IFNOTG$=""THEN4020
4019 PRINT"*{left}";:FORI=1TO100:NEXT:GOTO4017
4020 IFASC(G$)=13THEN4030
4021 IFASC(G$)=20ANDLEN(I$)>0THENI$=MID$(I$,1,LEN(I$)-1):PRINT"{left} {left}";:GOTO4019
4022 IFG$="'"THEN4024
4023 IFASC(G$)<65ORASC(G$)>90THEN4019
4024 I$=I$+G$:PRINTG$;:IFLEN(I$)>13THEN4030
4025 GOTO4019
4030 IFNOTI$=C$(C)THEN4040
4031 GOSUB600:GOSUB602:C=C+1:FORI=1TO100:NEXT:GOSUB605:IFC=CCTHEN5000
4032 PRINT"{home}{down:4}"R1$R1$R1$:PRINT"{home}{down:10}"R1$R1$R1$R1$R1$"{up:4}{right:11}GOOD!"
4033 FORI=1TO1000:NEXT:PRINT"{home}{down:9}"R1$R1$R1$"{up}"SPC(22)"             "
4034 I$="":GOTO4001
4040 PRINT"{home}{down:4}"R1$R1$R1$:PRINT"{home}{down:10}"R1$R1$R1$R1$R1$"{up:4}"SPC(22);
4041 PRINT"              ":I$="":TT=TT+50
4042 GOSUB77:FORI=1TO500:NEXT:GOSUB80:GOSUB73:GOSUB78:GOTO4016
5000 PRINT"{clear}{down}{right:4}{blue}IF YOU WOULD LIKE MORE PRACTICE{right:9}ON THE"CC"WORDS YOU ";
5001 IFCC=1THENPRINT"{left:6} YOU ";
5002 PRINT"MISSED, THEN":PRINT"{right:4}COPY THEM";:IFCC=1THENPRINT"{left:4}IT  {left:2}";
5003 PRINT" DOWN AND STUDY ON YOUR":PRINT"{right:4}OWN.{black}"
5005 FORI=0TO10STEP2:PRINT"{down}{right:4}"C$(I):PRINT"{up}"SPC(20)C$(I+1):NEXT
5006 PRINT"{down:4}{right:4}{blue}PUSH {red}{rvrs on}RETURN{rvrs off}{blue} TO START AGAIN."
5007 GETG$:IFG$=""THEN5007
5008 IFNOTASC(G$)=13THEN5007
5009 PRINT"{clear}":RUN
6000 PRINT"{clear}{green}{down:4}{right:10}{rvrs on}   {right} {right} {right}   {right}   {right}   "SPC(21)" {right:3} {right} {right} {right} {right} {right:3} {right} "
6001 PRINT"{right:10}{rvrs on}   {right} {right} {right}   {right}  {right:2}   "SPC(21)"{right:2} {right} {right} {right} {right:3} {right:3} {rvrs off}{$df}{rvrs on}{$df}"
6002 PRINT"{right:10}{rvrs on}   {right}   {right} {right:3}   {right} {right}{rvrs off}{$df}{down}"
6003 PRINT"{right:5}{rvrs on}   {right}   {right}   {right} {right:3} {right:3}   {right}   {right} "
6004 PRINT"{right:5}{rvrs on} {right:3} {right} {right} {right:3} {right:3} {right:3} {right:3} {right} {right} "
6005 PRINT"{right:5}{rvrs on}   {right}   {right}  {right:2} {right:3} {right:3}  {right:2}   {right} "
6006 PRINT"{right:5}{rvrs on}{right:2} {right} {right:3} {right:3} {right:3} {right:3} {right:3} {rvrs off}{$df}{rvrs on}{$df}"
6007 PRINT"{right:5}{rvrs on}   {right} {right:3}   {right}   {right}   {right}   {right} {right}{rvrs off}{$df}{right}{rvrs on} {home}{black}"
6008 GOSUB600:J=64:FORI=0TO37:J=J+1:IFJ=91THENJ=65
6009 GOSUB607:PRINTCHR$(J);:NEXT:FORI=0TO21:J=J+1:IFJ=91THENJ=65
6010 GOSUB607:PRINTCHR$(J)"{down}{left}";:NEXT:FORI=0TO37:J=J+1:IFJ=91THENJ=65
6011 GOSUB607:PRINTCHR$(J)"{left:2}";:NEXT:FORI=0TO21:J=J+1:IFJ=91THENJ=65
6012 GOSUB607:PRINTCHR$(J)"{up}{left}";:NEXT:GOSUB605:IFE+E1=0THENT=0:GOTO6021
6013 PRINT"{home}{down:18}{right:11}{blue}PUSH {red}{rvrs on}RETURN{rvrs off} {blue}TO SEE"SPC(60);
6014 PRINT"WHICH WORDS YOU MISSED.":T=0
6015 T=T+1:IFT=500THEN6020
6016 GETG$:IFG$=""THEN6015
6017 IFASC(G$)=13THENPRINT"{home}{down:18}"B$"{down}"B$:GOSUB73:GOTO3000
6018 GOTO6015
6020 PRINT"{clear}":RUN
6021 FORI=1TO1000:NEXT:GOTO6020
10000 DATA"AND","ANDE","BOOK","BOKK","GIRL","GRIL","SONG","SONGE","SEEN"
10001 DATA"SENE","SOME","SOM","THINK","THENK","WIN","WINN","VERY","VEREY"
10002 DATA"AROUND","ARUOND","READ","READE","WINTER","WINNTER","TABLE","TABBLE"
10003 DATA"EAST","EASTE","DROP","DROPP","SAID","SIAD","BEEN","BENN","FUNNY"
10004 DATA"FUNNE","USED","YUSED","BOXES","BOXS","DOES","DOS","COUNTING"
10005 DATA"COUNTTING","WAGON","WAGGON","HUNDRED","HUNDERD","GOES","GOSE"
10006 DATA"AFRAID","AFRIAD","VISITING","VISITTING","STORIES","STORYS","RAINY"
10007 DATA"RAINEY","SMOOTH","SMOOTHE","SKIRT","SKRIT","STUDIES","STUDYS"
10008 DATA"ANKLE","ANKEL","CHERRIES","CHERRYS","LARGELY","LARGELLY","CERTAIN"
10009 DATA"CERTIAN","STRAWBERRIES","STRAWBERRYS","SPECIAL","SPECIALL"
10010 DATA"PURPOSE","PERPOSE","ABLE","ABEL","ABOVE","ABBOVE","ACROSS","ACCROSS"
10011 DATA"AFTERNOON","AFTERNOONE","AIRPLANE","AIREPLANE","ANGRY","ANGERY"
10012 DATA"ANYTHING","ANYTHIN","ARROW","AROW","APPLE","APPEL","BADLY","BADDLY"
10013 DATA"BASEBALL","BASEBAL","BASKET","BASKIT","BECAME","BEECAME","BEFORE"
10014 DATA"BEFOR","BELOW","BELOWW","BICYCLE","BICICLE","BLOCK","BLOK"
10015 DATA"BOTTOM","BOTTOMM","BREAD","BIRED","BREAKFAST","BREAKFEST","BRIDGE"
10016 DATA"BRIGE","BRIGHT","BRITE","BROOM","BROME","CAMEL","CAMLE"
10017 DATA"CANNOT","CANOT","CAREFULLY","CAREFULY","CARRIED","CARRYED","CATCH"
10018 DATA"CACH","CHARGE","CHARG","CHEESE","CHEEZE","CHILDREN","CHIDLREN"
10019 DATA"CHIMNEY","CHIMMEY","CHURCH","CHIRCH","CLEAR","CLEARE","CLIMB"
10020 DATA"CLIME","SURPRISE","SURPRIZE","CLUB","CLUBB","COMPANY","COMMPANY"
10021 DATA"COUNTY","COUNNTY","COVER","COVVER","CREAM","CREAME","DANGER"
10022 DATA"DAGNER","DECIDE","DESIDE","DIFFERENT","DIFFRENT","DOLLAR","DOLAR"
10023 DATA"DOZEN","DOSEN","DURING","DURRING","EARTH","EARTHE","EIGHT","AIGHT"
10024 DATA"ELECTRIC","ALLECTRIC","ELEVEN","ALEVEN","ENEMY","EMENY","EVERYBODY"
10025 DATA"EVERYBODDY","EXCUSE","EXCUZE","FAIRY","FAIRRY","FEAST","FESTE"
10026 DATA"FENCE","FENSE","FIFTEEN","FIFTENE","FIFTY","FIFTEY","FIGHT"
10027 DATA"FITE","EVERY","EVEREY","FINGER","FINDGER","FOLLOW","FOLOW","FORGET"
10028 DATA"FORGETT","FRESH","FRECH","FRIEND","FREIND","BECAUSE","BECUASE"
10029 DATA"FRIGHTEN","FRITEN","FRUIT","FRIUT","FURNITURE","FURNICHURE","GLASS"
10030 DATA"GLASSE","GRANDFATHER","GRANFATHER","HAPPEN","HAPPIN","HEALTH"
10031 DATA"HEATLH","HEAVY","HEAVVY","HIDE","HYDE","HIGHER","HIER","HIMSELF"
10032 DATA"HEMSELF","INSTEAD","INSTAED","INTERESTING","INTERRESTIN","JUMP"
10033 DATA"JUMMP","KEEPING","KEPING","KNOCK","NOCK","LADY","LADIE"
10034 DATA"LANGUAGE","LANGWAGE","LAUGH","LAFF","LEAST","LEEST","LIFE","LIFFE"
10035 DATA"LIVING","LIVVING","LOOKING","LOOKKING","MATCH","MACH","MIDDLE"
10036 DATA"MIDLE","MONKEY","MONKY","MORNING","MORNIN","MOUNTAIN","MOUNTAN"
10037 DATA"MOUTH","MOWTH","MUSIC","MUZIC","NONE","NUNE","OCEAN","OSHUN","NORTH"
10038 DATA"NORTHE","OFTEN","OFEN","ONCE","WONCE","ORANGE","ORANG","OTHERS"
10039 DATA"OTHURS","PENCIL","PENSIL","PEOPLE","PEEPLE","PIANO","PAINO"
10040 DATA"PLEASE","PLAEZE","POLICE","POLISE"
10041 DATA"POPCORN","PACORN","PRESIDENT","PREZIDENT","PRIZE","PRISE","READY"
10042 DATA"READDY","REASON","REAZON","REMEMBER","REMEMMBER","ROOF","ROF","RUG"
10043 DATA"RUGG","RUNNING","RUNING","SAME","SAIM","SEAT","SETE","SHAPE","SHAP"
10044 DATA"SHARE","CHARE","SHELL","SHEL","SINCE","SINSE","SMOKE","SMOK"
10045 DATA"SNOWBALL","SNOWBAL","SOIL","SOOL","SOUND","SOWND","SQUIRREL"
10046 DATA"SKWIRREL","STAIRS","STERS","STOOD","STODE","STRAIGHT","STRATE"
10047 DATA"HOUSE","HOWSE","STRING","STRINK","SUGAR","SHUGAR","SUPPOSE"
10048 DATA"SPOSE","SWIMMING","SWIMING","TEACHER","TEAHER","TEETH","TEETHE"
10049 DATA"TIRED","TRID","TOMORROW","TOMMOROW","TRULY","TRUELY","VACATION"
10050 DATA"VACASHUN","VEGETABLE","VEGTABLE","WHEEL","WEEL","WISE","WIZE"
10051 DATA"WITHOUT","WITHOWT","WORLD","WORLLD","WRITING","WRITEING","YOURSELF"
10052 DATA"YURSELF","ANIMAL","AMINAL","AREN'T","ARN'T","ASLEEP","ASSLEEP"
10053 DATA"BEAUTIFUL","BEUTIFULL","PRETTY","PRITTY","BELIEVE","BELEVE"
10054 DATA"BETWEEN","BETWEN","BOARD","BORD","BUILD","BIULD","BUSY","BUZY"
10055 DATA"CARRY","CARY","CASTLE","CASSLE","CHANGE","CHANG","CHASE","CHACE"
10056 DATA"CHIEF","CHEIF","CHRISTMAS","CRISTMAS","DECEMBER","DESEMBER","DISHES"
10057 DATA"DISHS","EASTER","ESTER","EITHER","IETHER","ELEPHANT","ELEFANT"
10058 DATA"JANUARY","JANARY","FEBRUARY","FEBRARY","MARCH","MARRCH","APRIL"
10060 DATA"APPRIL","JUNE","JUN","JULY","JULI","AUGUST","AUGIST","SEPTEMBER"
10061 DATA"SETEMBER","OCTOBER","OCKTOBER","NOVEMBER","NOVEMER","ENOUGH","ENUF"
10062 DATA"ENJOY","ENGOY","SAW","SAWW","BOX","BOKS","MINE","MIINE"
10063 DATA"ACCEPTABLE","ACCEPTIBLE","ACCIDENTALLY","ACCIDENTLY","ACCOMMODATE"
10064 DATA"ACOMMODATE","ACQUIRE","AQUIRE","ACQUIT","AQUIT","AMATEUR","AMATURE"
10065 DATA"APPARENT","APARENT","CHANGEABLE","CHANGABLE","EMBARRASS","EMBARRAS"
10066 DATA"EXCEED","EXCEDE","HUMOROUS","HUMORUS","INOCULATE","INNOCULATE"
